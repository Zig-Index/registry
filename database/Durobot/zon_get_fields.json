{
  "name": "zon_get_fields",
  "owner": "Durobot",
  "repo": "zon_get_fields",
  "description": "Utility functions to get field values from the abstract syntax tree generated from ZON (Zig Object Notation) text using Zig stdlib",
  "type": "package",
  "topics": [
    "zig",
    "ziglang",
    "zon",
    "zig-package"
  ],
  "stars": 13,
  "forks": 2,
  "watchers": 1,
  "updated_at": "2025-05-05T08:19:00Z",
  "minimum_zig_version": "0.12.0-dev.3097+5c0766b6c",
  "readme": "# zon_get_fields\n\nSeveral functions to facilitate the process of walking Abstract Syntax Trees (ASTs) generated from [ZON](https://github.com/ziglang/zig/issues/14290) text, and fetch the values of the fields from the AST. \n\nBut first you have to call `std.zig.Ast.parse` to tokenize and parse your ZON, creating the AST.\n\nUpdated to work with and tested with Zig **0.14.0-dev.3046+08d661fcf**.\n\nThe latest commit is not going to work with earlier versions of Zig 0.14.0-dev, because of the breaking changes in lib/std/builtin.zig (change of names of struct fields, like `Array.sentinel` to `sentinel_ptr` or `StructField.default_value` to `default_value_ptr`).\n\nSupport for older versions, like Zig **0.12.0**, **0.12.1**, **0.13.0**, and **0.14.0-dev.91+a154d8da8** was dropped because of [this breaking change in the standard library](https://github.com/ziglang/zig/commit/0fe3fd01ddc2cd49c6a2b939577d16b9d2c65ea9).\n\nIf you need a version of zon_get_fields that works with older Zig versions, [get this release](https://github.com/Durobot/zon_get_fields/releases/tag/v0.1-beta). It still probably won't work with Zig 0.11, but you're welcome to try it and report back, although I'm not too keen on backporting.\n\n**zon_get_fields** is licensed under the [the MIT License](https://en.wikipedia.org/w/index.php?title=MIT_License&useskin=vector). You are more than welcome to drop `zon_get_fields.zig` into your project (don't forget to `const zgf = @import(\"zon_get_fields.zig\");`), or you can use the Zig package manager:\n\n1. In your project's `build.zig.zon`, in `.dependencies`, add\n\n   ```zig\n   .zon_get_fields =\n   .{\n       .url = \"https://github.com/Durobot/zon_get_fields/archive/<GIT COMMIT HASH, 40 HEX DIGITS>.tar.gz\",\n       .hash = \"<ZIG PACKAGE HASH, 68 HEX DIGITS>\" // Use arbitrary hash, get correct hash from the error \n   }\n   ```\n\n2. In your project's `build.zig`, in `pub fn build`, before `b.installArtifact(exe);`, add\n\n   ```zig\n   const zgf = b.dependency(\"zon_get_fields\",\n   .{\n       .target = target,\n       .optimize = optimize,\n   });\n   exe.root_module.addImport(\"zon_get_fields\", zgf.module(\"zon_get_fields\"));\n   ```\n\n3. Add `const zgf = @import(\"zon_get_fields\");`in your source file(s).\n\n3. Build your project with `zig build`, as you normally do.\n\nThings I like:\n\n1. Hey, it works! At least it looks like it does, and since nothing like this exists in the standard library, I consider it a success;\n2. You can throw your struct and an AST at a function (`pub fn zonToStruct`), and have your struct filled with data. The fields that have matching values in the provided AST, that is, and you get to know what was filled and what was not - look and the returned struct;\n3. Or you can fetch the values of the fields you need using string paths to indicate them, calling `pub fn getFieldVal`. It's fine if all you need is a couple of fields;\n3. All the basics are covered - signed and unsigned integers and floats, strings, single characters, booleans, nested structs, arrays, arrays of arrays, arrays of structs (in fact, any combination of structs and arrays should work).\n\nThings I don't really like:\n\n1. No AST validity verification. Thinking about ZON schemas or similar in the future, maybe?\n2. If you use the second approach, `pub fn getFieldVal`, for every field you want to read you've got to specify the path. The path is split, the AST is walked, and if you have rather large structures to fetch, a lot of this work is repeated for each field. Not the best approach performance-wise. But you can switch to `pub fn zonToStruct`, which fills all the fields it can it one go;\n3. An ugly hack that I had to use in order to get negative values from the fields, both integers and floating point. See `fn fulllTokenSlice`.  I can't be sure my approach works correctly in all situations, or will continue to work in the future, so I feel really uneasy about using it;\n\nFor examples of how to use them, turn to the test sections in `zon_parse.zig`:\n\n1. Find `zonToStruct Tests` [comment](https://github.com/Durobot/zon_get_fields/blob/cd1524a1e30b1375524a48b491d845f27a3b4594/src/zon_get_fields.zig#L1490) for `pub fn zonToStruct` approach - filling your struct all at once;\n2. Find `getFieldVal Tests` [comment](https://github.com/Durobot/zon_get_fields/blob/cd1524a1e30b1375524a48b491d845f27a3b4594/src/zon_get_fields.zig#L428) for `pub fn getFieldVal`approach - fetching field values one by one, as you provide string paths to each field.\n\nOr check out the short examples below (you can compile and run them with `zig build example_zon_to_struct` and `zig build example_get_field_val` commands).\n\nSay you've got this ZON file (`my.zon`):\n\n```zon\n.{\n    .database =\n    .{\n        .host = \"127.0.0.1\",\n        .port = 5432,\n        .user = \"superadmin\",\n        .password = \"supersecret\",\n    },\n    .decimal_separator = '.',\n    .months_in_year = 12,\n    .newline_char = '\\n',\n    .pi = 3.14159265359,\n    .hello = \"こんにちは\",\n    .unicode_char = '⚡',\n    .primes = .{ 2, 3, 5, 7, 11 },\n    .factorials = .{ 1, 1, 2, 6, 24, 120, 720, 5040, 40320, 362880, 3628800, 39916800 },\n    .slc_of_structs = .{ .{ .ham = 10, .eggs = 2 }, .{ .ham = 5 } },\n    .slc_of_arrays = .{ .{ 10, 20, 30 }, .{ 40, 50, }, },\n    .slc_of_slices = .{ .{ 1, 2, 3 }, .{ 4, 5 }, .{ 6 } }\n}\n```\nThen you can either:\n1. Define a struct, pass a pointer to it together with AST built from `my.zon` to pub `fn zonToStruct`, get your struct filled, and get a report struct which mirrors the fields of your struct, indicating whether they were filled or not (see [this table](#Report-struct-field-types) for report struct field types):\n\n```zig\nconst std = @import(\"std\");\nconst zgf = @import(\"zon_get_fields.zig\");\n\npub fn main() !void\n{\n    var gpa = std.heap.GeneralPurposeAllocator(.{}){};\n    defer _ = gpa.deinit();\n    const allocr = gpa.allocator();\n\n    // `Ast.parse` requires a sentinel (0) terminated slice, so we pass 0 as the sentinel value (last arg)\n    const zon_txt = try std.fs.cwd().readFileAllocOptions(allocr, \"my.zon\", std.math.maxInt(usize),\n                                                          null, @alignOf(u8), 0);\n    defer allocr.free(zon_txt);\n\n    var ast = try std.zig.Ast.parse(allocr, zon_txt, .zon);\n    defer ast.deinit(allocr);\n\n    const MyStruct = struct // Field order is not important\n    {\n        const DatabaseSettings = struct\n        {\n            host: []const u8 = &.{}, // Since this is a slice of _const_ u8,\n                                     // it will be addressing the string in ast.source\n            port: u16 = 0,\n            user: []u8 = &.{}, // Having a slice field requires passing a non-null allocator\n                               // to zonToStruct, and freeing `user` when we're done\n            password: [20]u8 = [_]u8 {33} ** 20, // This array is filled with string/array elements up\n                                                 // to this field's capacity, or padded with 0 bytes,\n                                                 // if ZON string/array hasn't got enough elements\n        };\n        const MyStruct = struct { ham: u32 = 0, eggs: u32 = 0 };\n        database: DatabaseSettings = .{},\n        decimal_separator: u8 = 0,\n        months_in_year: u8 = 0,\n        newline_char: u8 = 0,\n        pi: f64 = 0.0,\n        hello: []const u8 = &.{}, // Same as hello: []const u8 = undefined; hello.len = 0;\n        unicode_char: u21 = 0,\n        primes: [10]u8 = [_]u8 { 0 } ** 10,\n        factorials: [10]u32 = [_]u32 { 0 } ** 10,\n        slc_of_structs: []MyStruct = &.{},\n        slc_of_arrays: [][3]u32 = &.{},\n        slc_of_slices: [][]u32 = &.{},\n    };\n    var ms = MyStruct {};\n    // We MUST provide `allocr` if there are slices in ms, otherwise a null is fine.\n    const report = try zgf.zonToStruct(&ms, ast, allocr);\n    defer allocr.free(ms.database.user); // Must free `user` since it's a slice of non-const u8's.\n    defer allocr.free(ms.slc_of_structs);\n    defer allocr.free(report.slc_of_structs); // Must also free the slice of structs in `report`\n    defer allocr.free(ms.slc_of_arrays);\n    defer allocr.free(report.slc_of_arrays);\n    defer\n    {\n        for (ms.slc_of_slices) |s| allocr.free(s); // Deallocate nested slices first\n        allocr.free(ms.slc_of_slices); // THEN deallocate the outer slice\n        allocr.free(report.slc_of_slices); // `report` contains a slice of `ZonFieldResult` enums\n    }\n\n    std.debug.print(\"Field = database.host      value = {s}\\n\", .{ ms.database.host });\n    std.debug.print(\"Field = database.port      value = {d}\\n\", .{ ms.database.port });\n    std.debug.print(\"Field = database.user      value = {s}\\n\", .{ ms.database.user });\n    std.debug.print(\"Field = database.password  value = {s}\\n\", .{ ms.database.password });\n    std.debug.print(\"Field = decimal_separator  value = {c}\\n\", .{ ms.decimal_separator });\n    std.debug.print(\"Field = months_in_year     value = {d}\\n\", .{ ms.months_in_year });\n    std.debug.print(\"Field = newline_char       value = 0x{X:0>2}\\n\", .{ ms.newline_char });\n    std.debug.print(\"Field = pi                 value = {d}\\n\", .{ ms.pi });\n    std.debug.print(\"Field = hello              value = {s}\\n\", .{ ms.hello });\n    std.debug.print(\"Field = unicode_char       value = {u}\\n\", .{ ms.unicode_char });\n    std.debug.print(\"Field = primes             value = [ \", .{});\n    for (ms.primes) |p| std.debug.print(\"{}, \", .{ p });\n    std.debug.print(\"]\\n\", .{});\n    std.debug.print(\"Field = factorials         value = [ \", .{});\n    for (ms.factorials) |f| std.debug.print(\"{}, \", .{ f });\n    std.debug.print(\"]\\n\", .{});\n    std.debug.print(\"Field = slc_of_structs     value = [ \", .{});\n    for (ms.slc_of_structs) |s| std.debug.print(\"{{ ham = {}, eggs = {} }}, \", .{ s.ham, s.eggs });\n    std.debug.print(\"]\\n\", .{});\n    std.debug.print(\"Field = slc_of_arrays      value = [ \", .{});\n    for (ms.slc_of_arrays) |a|\n    {\n        std.debug.print(\"[ \", .{});\n        for (a) |e| std.debug.print(\"{}, \", .{ e });\n        std.debug.print(\"], \", .{});\n    }\n    std.debug.print(\"]\\n\", .{});\n    std.debug.print(\"Field = slc_of_slices      value = [ \", .{});\n    for (ms.slc_of_slices) |s|\n    {\n        std.debug.print(\"[ \", .{});\n        for (s) |e| std.debug.print(\"{}, \", .{ e });\n        std.debug.print(\"], \", .{});\n    }\n    std.debug.print(\"]\\n\\n\", .{});\n\n    // `report` describes the state of corresponding fields in `ms`\n    std.debug.print(\"Report =\\n{s}\\n\", .{ std.json.fmt(report, .{ .whitespace = .indent_4 }) });\n}\n```\n\n2. Or you can fetch the values one by one using `pub fn getFieldVal`:\n\n```zig\nconst std = @import(\"std\");\nconst zgf = @import(\"zon_get_fields.zig\");\n\npub fn main() !void\n{\n    var gpa = std.heap.GeneralPurposeAllocator(.{}){};\n    defer _ = gpa.deinit();\n    const allocr = gpa.allocator();\n\n    // `Ast.parse` requires a sentinel (0) terminated slice, so we pass 0 as the sentinel value (last arg)\n    const zon_txt = try std.fs.cwd().readFileAllocOptions(allocr, \"my.zon\", std.math.maxInt(usize),\n                                                          null, @alignOf(u8), 0);\n    defer allocr.free(zon_txt);\n\n    var ast = try std.zig.Ast.parse(allocr, zon_txt, .zon);\n    defer ast.deinit(allocr);\n\n    var fld_name: []const u8 = \"database.host\";\n    const dbhost_str = try zgf.getFieldVal([]const u8, ast, fld_name);\n    std.debug.print(\"Field = {s}      value = {s}\\n\", .{ fld_name, dbhost_str });\n\n    fld_name = \"database.port\";\n    const dbport_u16 = try zgf.getFieldVal(u16, ast, fld_name);\n    std.debug.print(\"Field = {s}      value = {d}\\n\", .{ fld_name, dbport_u16 });\n\n    fld_name = \"database.user\";\n    const dbhost_user = try zgf.getFieldVal([]const u8, ast, fld_name);\n    std.debug.print(\"Field = {s}      value = {s}\\n\", .{ fld_name, dbhost_user });\n\n    fld_name = \"database.password\";\n    const dbhost_password = try zgf.getFieldVal([]const u8, ast, fld_name);\n    std.debug.print(\"Field = {s}  value = {s}\\n\", .{ fld_name, dbhost_password });\n\n    fld_name = \"decimal_separator\";\n    const dec_separ_u8 = try zgf.getFieldVal(u8, ast, fld_name);\n    std.debug.print(\"Field = {s}  value = {c}\\n\", .{ fld_name, dec_separ_u8 });\n\n    fld_name = \"months_in_year\";\n    const months_in_year_u8 = try zgf.getFieldVal(u8, ast, fld_name);\n    std.debug.print(\"Field = {s}     value = {d}\\n\", .{ fld_name, months_in_year_u8 });\n\n    fld_name = \"newline_char\";\n    const newline_char_u8 = try zgf.getFieldVal(u8, ast, fld_name);\n    std.debug.print(\"Field = {s}       value = 0x{X:0>2}\\n\", .{ fld_name, newline_char_u8 });\n\n    fld_name = \"pi\";\n    const pi_f64 = try zgf.getFieldVal(f64, ast, fld_name);\n    std.debug.print(\"Field = {s}                 value = {d}\\n\", .{ fld_name, pi_f64 });\n\n    fld_name = \"hello\";\n    const hello_unicode_str = try zgf.getFieldVal([]const u8, ast, fld_name);\n    std.debug.print(\"Field = {s}              value = {s}\\n\", .{ fld_name, hello_unicode_str });\n\n    fld_name = \"unicode_char\";\n    const unicode_char_u21 = try zgf.getFieldVal(u21, ast, fld_name);\n    std.debug.print(\"Field = {s}       value = {u}\\n\", .{ fld_name, unicode_char_u21 });\n\n    std.debug.print(\"Field = primes             value = [ \", .{});\n    var buf = [_]u8 { 0 } ** 22;\n    for (0..5) |i|\n    {\n        const buf_slice = try std.fmt.bufPrint(&buf, \"primes[{d}]\", .{i});\n        const int_u8 = try zgf.getFieldVal(u8, ast, buf_slice);\n        std.debug.print(\"{d}, \", .{ int_u8 });\n    }\n    std.debug.print(\"]\\n\", .{});\n\n    std.debug.print(\"Field = factorials         value = [ \", .{});\n    for (0..12) |i|\n    {\n        const buf_slice = try std.fmt.bufPrint(&buf, \"factorials[{d}]\", .{i});\n        const int_u32 = try zgf.getFieldVal(u32, ast, buf_slice);\n        std.debug.print(\"{d}, \", .{ int_u32 });\n    }\n    std.debug.print(\"]\\n\", .{});\n\n    std.debug.print(\"Field = slc_of_structs     value = [ \", .{});\n    for (0..2) |i|\n    {\n        const ham_buf_slice = try std.fmt.bufPrint(&buf, \"slc_of_structs[{d}].ham\", .{i});\n        const ham_int_u32 = try zgf.getFieldVal(u32, ast, ham_buf_slice);\n        const eggs_buf_slice = try std.fmt.bufPrint(&buf, \"slc_of_structs[{d}].eggs\", .{i});\n        const eggs_int_u32 = zgf.getFieldVal(u32, ast, eggs_buf_slice) catch |err|\n        {   // We actually must check these things for all fields\n            std.debug.print(\"{{ ham = {d}, eggs = {} }}, \", .{ ham_int_u32, err });\n            continue;\n        };\n        std.debug.print(\"{{ ham = {d}, eggs = {d} }}, \", .{ ham_int_u32, eggs_int_u32 });\n    }\n    std.debug.print(\"]\\n\", .{});\n\n    std.debug.print(\"Field = slc_of_arrays      value = [ \", .{});\n    for (0..2) |i|\n    {\n        std.debug.print(\"[ \", .{});\n        for (0..(3 - i)) |j|\n        {\n            const buf_slice = try std.fmt.bufPrint(&buf, \"slc_of_arrays[{d}].[{d}]\", .{i, j});\n            const int_u32 = try zgf.getFieldVal(u32, ast, buf_slice);\n            std.debug.print(\"{d}, \", .{ int_u32 });\n        }\n        std.debug.print(\"], \", .{});\n    }\n    std.debug.print(\"]\\n\", .{});\n\n    std.debug.print(\"Field = slc_of_slices      value = [ \", .{});\n    for (0..3) |i|\n    {\n        std.debug.print(\"[ \", .{});\n        for (0..(3 - i)) |j|\n        {\n            const buf_slice = try std.fmt.bufPrint(&buf, \"slc_of_slices[{d}].[{d}]\", .{i, j});\n            const int_u32 = try zgf.getFieldVal(u32, ast, buf_slice);\n            std.debug.print(\"{d}, \", .{ int_u32 });\n        }\n        std.debug.print(\"], \", .{});\n    }\n    std.debug.print(\"]\\n\", .{});\n}\n```\n\n\n\n##### Report struct field types\n\n| Target struct field                      | Report struct field                                          |\n| ---------------------------------------- | ------------------------------------------------------------ |\n| Primitive types (integers, floats, etc.) | ZonFieldResult enum.                                         |\n| Array of elements of a primitive type    | Array of ZonFieldResult enum elements.                       |\n| Array of arrays, structs or slices       | Array of arrays, structs, slices, or ZonFieldResult enums (for target array of slices of primitive elements).<br />The type of report array element depends on the type of target array elements. |\n| Struct                                   | Struct. Field types depend on target struct field types.     |\n| Slice of elements of a primitive type    | ZonFieldResult enum.                                         |\n| Slice of arrays                          | Slice of arrays. Report array element type depends on the target array element type.<br />**Note**: (1) allocator must be provided (not null), since report slice has to be allocated; (2) caller owns the report struct and must deallocate this report slice. |\n| Slice of structs                         | Slice of structs. Report struct field types depend on the target struct field types.<br />**Note**: (1) allocator must be provided (not null), since report slice has to be allocated; (2) caller owns the report struct and must deallocate this report slice. |\n| Slice of slices                          | If target nested slices contain primitive type elements (integers, floats, etc.), then the report will contain a slice of ZonFieldResult enum elements.<br />If target nested slices contain structs, arrays or slices, then the report will contain a slice of matching structs, arrays or slices. See target struct field types 'struct', 'array' or 'slice' above.<br />**Note**: (1) allocator must be provided (not null), since report slice has to be allocated; (2) caller owns the report struct and must deallocate this report slice. |",
  "owner_avatar_url": "https://avatars.githubusercontent.com/u/22280250?u=110d0be2f4ea1ffb652f8fdf0956dd42c0b81232&v=4",
  "releases": [
    {
      "tag_name": "v0.1-beta",
      "name": "Last version supporting Zig older than 0.14.0-dev.1421+f87dd43c1",
      "body": "This is the last version to support Zig older than 0.14.0-dev.1421+f87dd43c1, such as Zig **0.12.0**, **0.12.1**, **0.13.0**, and **0.14.0-dev.91+a154d8da8**.\r\n\r\nThe reason to stop supporting them is because of the [breaking change in Zig standard library](https://github.com/ziglang/zig/commit/0fe3fd01ddc2cd49c6a2b939577d16b9d2c65ea9), in std.builtin.Type - its fields were renamed.\r\n\r\nIf you need to use **zon_get_fields** with the current dev build of Zig (0.14.0-dev.1421+f87dd43c1 or later), do not use this version. Get the latest version instead.",
      "prerelease": false,
      "published_at": "2024-09-05T10:32:54Z",
      "html_url": "https://github.com/Durobot/zon_get_fields/releases/tag/v0.1-beta",
      "assets": []
    }
  ],
  "owner_company": null,
  "owner_location": "Chisinau, Republic of Moldova",
  "owner_blog": null,
  "owner_twitter_username": null,
  "owner_followers": 10,
  "owner_following": 3,
  "owner_created_at": "2016-09-18T18:41:24Z",
  "license": "MIT",
  "category": "library"
}